pipeline {
    agent {
        kubernetes {
            label 'k8s-deployer'
            defaultContainer 'deploy'
            yaml """
apiVersion: v1
kind: Pod
spec:
  serviceAccountName: jenkins
  containers:
  - name: deploy
    image: nazman/k8s-deployer:latest
    command:
    - cat
    tty: true
"""
        }
    }

    options {
        skipDefaultCheckout(true)
    }

    parameters {

    choice(
        name: 'DEPLOY_TARGET',
        choices: ['single', 'all'],
        description: 'Deploy to one cluster or all clusters'
    )

    choice(
        name: 'CLUSTER',
        choices: ['cloud', 'onprem'],
        description: 'Cluster to deploy (used if single selected)'
    )

    string(
        name: 'HELM_CHART_VERSION',
        defaultValue: '4.0.18',
        description: 'Helm chart version'
    )

    booleanParam(
        name: 'FORCE_DEPLOY',
        defaultValue: false,
        description: 'Force deployment'
    )

    string(
        name: 'HELM_CHART_DIR',
        defaultValue: 'helm-nfs-client-provisioner',
        description: 'Directory where chart files and values.yaml are located'
    )

    string(
        name: 'HELM_REPO_NAME',
        defaultValue: 'nfs-subdir-external-provisioner',
        description: 'Helm repository name'
    )

    string(
        name: 'HELM_REPO_URL',
        defaultValue: 'https://kubernetes-sigs.github.io/nfs-subdir-external-provisioner/',
        description: 'Helm repository URL'
    )

    string(
        name: 'HELM_RELEASE_NAME',
        defaultValue: 'nfs-client-provisioner',
        description: 'Helm release name'
    )

    string(
        name: 'HELM_CHART_NAME',
        defaultValue: 'nfs-subdir-external-provisioner/nfs-subdir-external-provisioner',
        description: 'Helm chart name'
    )

    string(
        name: 'VALUES_URL',
        defaultValue: 'nfs-provisioner-values.yaml',
        description: 'Values.yaml file name'
    )

    string(
        name: 'NAMESPACE',
        defaultValue: 'nfs-provisioner',
        description: 'Kubernetes namespace'
    )
}
    environment {
        APP_DIR = "${params.HELM_CHART_DIR}"
        VALUES_FILE = "${params.VALUES_URL}"
        NAMESPACE = "${params.NAMESPACE}"
        HELM_REPO_URL = "${params.HELM_REPO_URL}"
        HELM_RELEASE_NAME = "${params.HELM_RELEASE_NAME}"
        HELM_REPO_NAME = "${params.HELM_REPO_NAME}"
        HELM_CHART_NAME = "${params.HELM_CHART_NAME}"
    }

    stages {

        stage('Checkout') {
            steps {
                container('deploy') {
                    git branch: 'main',
                        url: 'https://github.com/nazmang/k8s.git'
                }
            }
        }

        stage('Deploy') {
            when {
                    anyOf {
                        changeset "**/${env.APP_DIR}/**"
                        expression { return params.FORCE_DEPLOY }
                    }
            }
            steps {
                
                script {

                    def clusterMap = [
                        "cloud": [
                            credential: "kubeconfig-hetzner",
                            envDir: "environments/cloud",
                            critical: true
                        ],
                        "onprem": [
                            credential: "kubeconfig-onprem",
                            envDir: "environments/onprem",
                            critical: false
                        ]
                    ]

                    def targets = params.DEPLOY_TARGET == "all" ?
                                  clusterMap.keySet() :
                                  [params.CLUSTER]

                    parallel targets.collectEntries { c ->

                        ["Deploy to ${c}": {

                            container('deploy') {

                                def cfg = clusterMap[c]

                                withCredentials([
                                    file(credentialsId: cfg.credential, variable: 'KUBECONFIG')
                                ]) {

                                    try {

                                        echo "Checking connectivity to ${c}"
                                        sh "kubectl cluster-info"

                                        // ===== Helm deploy =====
                                        dir("${APP_DIR}/${cfg.envDir}") {

                                            sh """
                                                helm repo add ${HELM_REPO_NAME} ${HELM_REPO_URL} || true
                                                helm repo update

                                                helm upgrade --install ${HELM_RELEASE_NAME} ${HELM_CHART_NAME} \
                                                --namespace ${NAMESPACE} \
                                                --create-namespace \
                                                --version ${params.HELM_CHART_VERSION} \
                                                -f ${VALUES_FILE}
                                            """
                                        }


                                        echo "Deployment to ${c} completed."

                                    } catch (err) {

                                        if (cfg.critical) {
                                            error("Critical cluster ${c} failed. Stopping pipeline.")
                                        } else {
                                            echo "Non-critical cluster ${c} failed. Skipping."
                                        }
                                    }
                                }
                            }
                        }]
                    }
                }
            }
        }
    }
}
