pipeline {
    agent {
        kubernetes {
            label 'k8s-deployer'
            defaultContainer 'deploy'
            yaml """
apiVersion: v1
kind: Pod
spec:
  serviceAccountName: jenkins
  containers:
  - name: deploy
    image: nazman/k8s-deployer:latest
    command:
    - cat
    tty: true
"""
        }
    }

    options {
        skipDefaultCheckout(true)
    }

    parameters {

        choice(
            name: 'DEPLOY_TARGET',
            choices: ['single', 'all'],
            description: 'Deploy to one cluster or all clusters'
        )

        choice(
            name: 'CLUSTER',
            choices: ['cloud', 'onprem'],
            description: 'Cluster to deploy (used if single selected)'
        )

        string(
            name: 'HELM_CHART_VERSION',
            defaultValue: '0.14.8',
            description: 'MetalLB Helm chart version'
        )

        booleanParam(
            name: 'FORCE_DEPLOY',
            defaultValue: false,
            description: 'Force deployment'
        )
    }

    environment {
        APP_DIR = "${params.HELM_CHART_DIR}"
        VALUES_FILE = "values.yaml"
        METALLB_CONFIG = "metallb-config.yaml"
        NAMESPACE = "metallb-system"
    }

    stages {

        stage('Checkout') {
            steps {
                container('deploy') {
                    git branch: 'main',
                        url: 'https://github.com/nazmang/k8s.git'
                }
            }
        }

        stage('Deploy') {
            when {
                    anyOf {
                        changeset "**/${env.APP_DIR}/**"
                        expression { return params.FORCE_DEPLOY }
                    }
            }
            steps {
                
                script {

                    def clusterMap = [
                        "cloud": [
                            credential: "kubeconfig-hetzner",
                            envDir: "environments/cloud",
                            critical: true
                        ],
                        "onprem": [
                            credential: "kubeconfig-onprem",
                            envDir: "environments/onprem",
                            critical: false
                        ]
                    ]

                    def targets = params.DEPLOY_TARGET == "all" ?
                                  clusterMap.keySet() :
                                  [params.CLUSTER]

                    parallel targets.collectEntries { c ->

                        ["Deploy to ${c}": {

                            container('deploy') {

                                def cfg = clusterMap[c]

                                withCredentials([
                                    file(credentialsId: cfg.credential, variable: 'KUBECONFIG')
                                ]) {

                                    try {

                                        echo "Checking connectivity to ${c}"
                                        sh "kubectl cluster-info"

                                        // ===== kube-proxy strictARP fix =====
                                        echo "Ensuring strictARP=true"

                                        sh """
                                            if ! kubectl get configmap kube-proxy -n kube-system -o yaml | grep -q "strictARP: true"; then
                                                kubectl get configmap kube-proxy -n kube-system -o yaml | \
                                                sed 's/strictARP: false/strictARP: true/' | \
                                                kubectl apply -f -
                                            fi
                                        """

                                        // ===== Helm deploy =====
                                        dir("${APP_DIR}/${cfg.envDir}") {

                                            sh """
                                                helm repo add metallb https://metallb.github.io/metallb || true
                                                helm repo update

                                                helm upgrade --install metallb metallb/metallb \
                                                --namespace ${NAMESPACE} \
                                                --create-namespace \
                                                --version ${params.HELM_CHART_VERSION} \
                                                -f ${VALUES_FILE}
                                            """

                                            // wait for CRDs
                                            sh """
                                            kubectl wait --for=condition=Established crd/ipaddresspools.metallb.io --timeout=60s
                                            kubectl wait --for=condition=Established crd/l2advertisements.metallb.io --timeout=60s
                                            kubectl wait --for=condition=Established crd/bgpadvertisements.metallb.io --timeout=60s
                                            """

                                            sh """
                                                kubectl apply -f ${METALLB_CONFIG} -n ${NAMESPACE}
                                            """
                                        }


                                        echo "Deployment to ${c} completed."

                                    } catch (err) {

                                        if (cfg.critical) {
                                            error("Critical cluster ${c} failed. Stopping pipeline.")
                                        } else {
                                            echo "Non-critical cluster ${c} failed. Skipping."
                                        }
                                    }
                                }
                            }
                        }]
                    }
                }
            }
        }
    }
}
